open Core
open Stlc

type atom =
  | Var of string
  | Float of float
  | Int of int
  | Bool of bool
  | Unit
[@@deriving sexp_of]

type term =
  | Atom of atom
  | Bop of Glsl.binary_op * atom * atom
  | Vec of int * atom list
  | Mat of int * int * atom list
  | Index of atom * int
  | Builtin of Glsl.builtin * atom list
  | App of atom * atom
  | If of atom * anf * anf
  | Lam of string * Stlc.ty * anf
[@@deriving sexp_of]

and anf =
  | Let of string * term * anf
  | Return of term
[@@deriving sexp_of]

type top =
  | Define of string * anf
  | Extern of ty * string
[@@deriving sexp_of]

type t = Program of Stlc.ty String.Map.t * top list [@@deriving sexp_of]

let type_of_atom ctx = function
  | Var v -> Map.find_exn ctx v
  | Float _ -> TyFloat
  | Int _ -> TyInt
  | Bool _ -> TyBool
  | Unit -> TyUnit
;;

let rec type_of_term ctx = function
  | Atom a -> type_of_atom ctx a
  | Bop (op, l, r) ->
    let ty_l = type_of_atom ctx l in
    let ty_r = type_of_atom ctx r in
    (match op, ty_l, ty_r with
     | (Add | Sub | Mul | Div | Mod), TyFloat, TyFloat -> TyFloat
     | (Add | Sub | Mul | Div | Mod), TyInt, TyInt -> TyInt
     | (Add | Sub), TyVec n, TyVec _ -> TyVec n
     | (Mul | Div), TyVec n, TyFloat | (Mul | Div), TyFloat, TyVec n -> TyVec n
     | Mul, TyVec n, TyVec _ -> TyVec n
     | (Add | Sub | Mul | Div | Mod), TyMat (x, y), TyMat (x', y') when x = x' && y = y'
       -> TyMat (x, y)
     | (Mul | Div), TyMat (x, y), TyFloat | (Mul | Div), TyFloat, TyMat (x, y) ->
       TyMat (x, y)
     | (Mul | Div), TyMat (x, y), TyVec n when y = n -> TyVec x
     | (Add | Sub | Mul | Div | Mod), _, _ -> failwith "get_term_ty: invalid bop"
     | (Lt | Gt | Leq | Geq | Eq | And | Or), _, _ -> TyBool)
  | Vec (n, _) -> TyVec n
  | Mat (x, y, _) -> TyMat (x, y)
  | Index (t, _) ->
    (match type_of_atom ctx t with
     | TyVec _ -> TyFloat
     | TyMat (_, y) -> TyVec y
     | _ -> failwith "get_term_ty: invalid index")
  | Builtin (name, args) ->
    let tys = List.map args ~f:(type_of_atom ctx) in
    let check_unary_math () =
      match tys with
      | [ TyFloat ] -> TyFloat
      | [ TyVec n ] -> TyVec n
      | _ -> failwith "get_term_ty: builtin unary math error"
    in
    let check_binary_math () =
      match tys with
      | [ TyFloat; TyFloat ] -> TyFloat
      | [ TyVec n; TyVec n' ] when n = n' -> TyVec n
      | [ TyVec n; TyFloat ] | [ TyFloat; TyVec n ] -> TyVec n
      | _ -> failwith "get_term_ty: builtin binary math error"
    in
    let check_geometric () =
      match name, tys with
      | (Length | Distance | Dot), _ -> TyFloat
      | Cross, [ TyVec 3; TyVec 3 ] -> TyVec 3
      | Normalize, [ TyVec n ] -> TyVec n
      | Normalize, [ TyFloat ] -> TyFloat
      | _ -> failwith "get_term_ty: builtin geometric error"
    in
    let check_common () =
      match name, tys with
      | (Abs | Sign | Floor | Ceil), _ -> check_unary_math ()
      | (Min | Max), _ -> check_binary_math ()
      | Clamp, [ TyFloat; TyFloat; TyFloat ] -> TyFloat
      | Clamp, [ TyVec n; TyFloat; TyFloat ] -> TyVec n
      | Clamp, [ TyVec n; TyVec _; TyVec _ ] -> TyVec n
      | Mix, [ TyFloat; TyFloat; TyFloat ] -> TyFloat
      | Mix, [ TyVec n; TyVec _; TyFloat ] -> TyVec n
      | Mix, [ TyVec n; TyVec _; TyVec _ ] -> TyVec n
      | _ -> failwith "get_term_ty: builtin common error"
    in
    (match name with
     | Sin | Cos | Tan | Asin | Acos | Atan | Exp | Log | Exp2 | Log2 | Sqrt ->
       check_unary_math ()
     | Pow -> check_binary_math ()
     | Length | Distance | Dot | Cross | Normalize -> check_geometric ()
     | Abs | Sign | Floor | Ceil | Min | Max | Clamp | Mix -> check_common ())
  | App (f, _) ->
    (match type_of_atom ctx f with
     | TyArrow (_, r) -> r
     | _ -> failwith "get_term_ty: invalid app")
  | If (_, t, _) -> type_of ctx t
  | Lam (v, ty_v, t) ->
    let map = Map.set ctx ~key:v ~data:ty_v in
    TyArrow (ty_v, type_of map t)

(* TODO: Typechecking stage should maybe occur after ANF? This feels redundant *)
and type_of ctx = function
  | Let (v, term, body) ->
    let ty_v = type_of_term ctx term in
    type_of (Map.set ctx ~key:v ~data:ty_v) body
  | Return term -> type_of_term ctx term
;;

(* TODO: Refactor to use some kind of State monad + let* for implicit CPS *)
let rec normalize (map : ty String.Map.t) (expr : Stlc.term) : ty String.Map.t * anf =
  match expr with
  | Var v -> map, Return (Atom (Var v))
  | Float f -> map, Return (Atom (Float f))
  | Int i -> map, Return (Atom (Int i))
  | Bool b -> map, Return (Atom (Bool b))
  | Unit -> map, Return (Atom Unit)
  | Lam (v, ty_v, t) ->
    let map = Map.set map ~key:v ~data:ty_v in
    let map, t = normalize map t in
    map, Return (Lam (v, ty_v, t))
  | Let (v, bind, body) ->
    let map, bind_anf = normalize map bind in
    let ty_v = type_of map bind_anf in
    let map = Map.set map ~key:v ~data:ty_v in
    let map, body_anf = normalize map body in
    let rec make_let = function
      | Let (v, bind, body) -> Let (v, bind, make_let body)
      | Return t -> Let (v, t, body_anf)
    in
    map, make_let bind_anf
  | App (f, x) ->
    atomize map f (fun map f -> atomize map x (fun map x -> map, Return (App (f, x))))
  | Bop (op, l, r) ->
    atomize map l (fun map l -> atomize map r (fun map r -> map, Return (Bop (op, l, r))))
  | Vec (n, ts) -> atomize_list map ts (fun map ts -> map, Return (Vec (n, ts)))
  | Mat (x, y, ts) -> atomize_list map ts (fun map ts -> map, Return (Mat (x, y, ts)))
  | Index (t, i) -> atomize map t (fun map t -> map, Return (Index (t, i)))
  | Builtin (f, args) ->
    atomize_list map args (fun map args -> map, Return (Builtin (f, args)))
  | If (c, t, e) ->
    atomize map c (fun map c ->
      let map, t_anf = normalize map t in
      let map, e_anf = normalize map e in
      map, Return (If (c, t_anf, e_anf)))

and atomize (map : ty String.Map.t) (expr : Stlc.term) k : ty String.Map.t * anf =
  match expr with
  | Var v -> k map (Var v)
  | Float f -> k map (Float f)
  | Int i -> k map (Int i)
  | Bool b -> k map (Bool b)
  | Unit -> k map Unit
  | _ ->
    let map, anf_block = normalize map expr in
    let ty = type_of map anf_block in
    let v = Utils.fresh "anf" in
    let map = Map.set map ~key:v ~data:ty in
    let map, rem = k map (Var v) in
    let rec make_let = function
      | Let (v, bind, body) -> Let (v, bind, make_let body)
      | Return t -> Let (v, t, rem)
    in
    map, make_let anf_block

and atomize_list map ts k =
  match ts with
  | [] -> k map []
  | t :: ts ->
    atomize map t (fun map t -> atomize_list map ts (fun map ts -> k map (t :: ts)))
;;

let normalize_top (map : ty String.Map.t) (t : Stlc.top) : ty String.Map.t * top =
  match t with
  | Define (v, bind) ->
    let map, bind_anf = normalize map bind in
    let ty_v = type_of map bind_anf in
    let map = Map.set map ~key:v ~data:ty_v in
    map, Define (v, bind_anf)
  | Extern (ty, v) -> map, Extern (ty, v)
;;

let to_anf (Program (map, terms) : Typecheck.t) : t =
  let map, anfs = List.fold_map terms ~init:map ~f:normalize_top in
  Program (map, anfs)
;;
