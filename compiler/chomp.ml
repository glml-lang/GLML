open Core
open Lexer

module Maybe = struct
  type error_info =
    { message : string
    ; found : (token * loc) option
    ; contexts : (string * loc option) list
    }

  let sexp_of_error_info { message; found; contexts } =
    let chomp_error =
      match found with
      | None -> message
      | Some (token, loc) ->
        let token = Sexplib.Sexp.to_string_hum (sexp_of_token token) in
        let loc = Sexplib.Sexp.to_string_hum (sexp_of_loc loc) in
        [%string "%{message} on <%{token}> %{loc}"]
    in
    let string_of_context = function
      | s, None -> s
      | s, Some loc ->
        let loc = Sexp.to_string_hum (sexp_of_loc (loc : loc)) in
        [%string "%{s} %{loc}"]
    in
    let contexts = List.rev (List.map ~f:string_of_context contexts) in
    [%message (chomp_error : string) (contexts : string list)]
  ;;

  type 'a t =
    | Success of 'a
    | Fail of error_info
    | Fatal of error_info

  let to_or_error = function
    | Fail e | Fatal e -> Error (Error.of_lazy_sexp (lazy (sexp_of_error_info e)))
    | Success x -> Ok x
  ;;

  include Applicative.Make (struct
      type nonrec 'a t = 'a t

      let apply mf mx =
        match mf with
        | Fail e -> Fail e
        | Fatal e -> Fatal e
        | Success f ->
          (match mx with
           | Fail e -> Fail e
           | Fatal e -> Fatal e
           | Success x -> Success (f x))
      ;;

      let return x = Success x
      let map = `Define_using_apply
    end)

  include Monad.Make (struct
      type nonrec 'a t = 'a t

      let bind m ~f =
        match m with
        | Success x -> f x
        | Fail e -> Fail e
        | Fatal e -> Fatal e
      ;;

      let return = return
      let map = `Define_using_bind
    end)
end

open Maybe

type 'a maybe = 'a Maybe.t

type stream =
  { seq : (token * loc) Sequence.t
  ; last_pos : pos
  }

type 'a t = stream -> ('a * stream) Maybe.t

include Applicative.Make (struct
    type nonrec 'a t = 'a t

    let apply pf px st =
      let open Maybe.Let_syntax in
      let%bind f, st' = pf st in
      let%bind x, st'' = px st' in
      Success (f x, st'')
    ;;

    let return x st = Success (x, st)
    let map = `Define_using_apply
  end)

include Monad.Make (struct
    type nonrec 'a t = 'a t

    let bind p ~f =
      fun st ->
      let%bind.Maybe a, st' = p st in
      f a st'
    ;;

    let return = return
    let map = `Define_using_bind
  end)

module Infix_syntax = struct
  include Applicative_infix

  let ( <$> ) f x = map ~f x
  let ( <$ ) f p = Fun.const f <$> p
  let ( $> ) p f = f <$ p
  let ( *> ) i p = ignore_m i *> p
  let ( <* ) p i = p <* ignore_m i

  let ( <*>| ) pf px =
    let open Maybe.Let_syntax in
    fun st ->
      let%bind f, st' = pf st in
      let%bind x, st'' = (Lazy.force px) st' in
      Success (f x, st'')
  ;;

  let ( <|> ) p p' =
    fun st ->
    match p st with
    | Success res -> Success res
    | Fatal e -> Fatal e
    | Fail _ ->
      (match p' st with
       | Success res -> Success res
       | Fatal e' -> Fatal e'
       | Fail e' -> Fail e')
  ;;

  let pos_of_stream st = Option.map ~f:(fun ((_, pos), _) -> pos) (Sequence.next st.seq)

  let ( <??> ) p tag =
    fun st ->
    match p st with
    | Success res -> Success res
    | Fatal e -> Fatal { e with contexts = (tag, pos_of_stream st) :: e.contexts }
    | Fail e -> Fail { e with contexts = (tag, pos_of_stream st) :: e.contexts }
  ;;
end

open Let_syntax
open Infix_syntax

let fail ?pos ?tok message = Fail { message; found = Option.both tok pos; contexts = [] }

let commit p =
  fun st ->
  match p st with
  | Success v -> Success v
  | Fail e | Fatal e -> Fatal e
;;

let satisfy (pred : token -> bool) : token t =
  fun st ->
  match Sequence.next st.seq with
  | Some ((tok, ((_, last_pos) as pos)), seq) ->
    if pred tok then Success (tok, { seq; last_pos }) else fail ~pos ~tok "satisfy_fail"
  | None -> fail "satisfy_eof"
;;

let peek : token t =
  fun st ->
  match Sequence.next st.seq with
  | Some ((c, _), _) -> Success (c, st)
  | None -> fail "peek_eof"
;;

let satisfy_map (pred : token -> 'a option) : 'a t =
  fun st ->
  match Sequence.next st.seq with
  | None -> fail "satisfy_map_eof"
  | Some ((c, ((_, last_pos) as pos)), seq) ->
    (match pred c with
     | Some c -> Success (c, { seq; last_pos })
     | None -> fail ~pos "satisfy_map_fail")
;;

let rec many1 p = List.cons <$> p <*>| lazy (many p)
and many p = many1 p <|> return []

let sep_by1 sep p = List.cons <$> p <*> many (sep *> p)
let sep_by sep p = sep_by1 sep p <|> return []

let chainl1 (p : 'a t) (op : ('a -> 'a -> 'a) t) : 'a t =
  let rec go acc =
    (let%bind f = op in
     let%bind rhs = p in
     go (f acc rhs))
    <|> return acc
  in
  let%bind lhs = p in
  go lhs
;;

let run p s =
  Maybe.to_or_error
    (let last_pos = { i = 0; line = 0; col = 0 } in
     (* TODO: move the above to [Lexer]? *)
     let%bind.Maybe x, st = p { seq = Sequence.of_list s; last_pos } in
     match Sequence.next st.seq with
     | Some ((_, pos), _) -> fail ~pos "run_stream_not_fully_consumed"
     | None -> Success x)
;;

let tok t = satisfy (equal_token t)
let fail message = Fn.const (Fail { message; found = None; contexts = [] })
let fatal message = Fn.const (Fatal { message; found = None; contexts = [] })

let with_loc (p : 'a t) : ('a * loc) t =
  fun st ->
  let start_pos =
    match Sequence.next st.seq with
    | Some ((_, (start_p, _)), _) -> start_p
    | None -> st.last_pos
  in
  match p st with
  | Success (v, st) -> Success ((v, (start_pos, st.last_pos)), st)
  | Fail e -> Fail e
  | Fatal e -> Fatal e
;;
