open Core
open Glsl

let to_glsl_ty (ty : Stlc.ty) : ty =
  match ty with
  | TyFloat -> TyFloat
  | TyInt -> TyInt
  | TyBool -> TyBool
  | TyVec n -> TyVec n
  | TyMat (x, y) -> TyMat (x, y)
  | TyArrow _ -> failwith "translate: arrow types should not be translated"
;;

let to_glsl_atom (a : Anf.atom) : term =
  match a with
  | Var v -> Var v
  | Float f -> Float f
  | Int i -> Int i
  | Bool b -> Bool b
;;

let to_glsl_term (t : Anf.term) : term =
  match t.desc with
  | Atom a -> to_glsl_atom a
  | Bop (op, l, r) -> Bop (op, to_glsl_atom l, to_glsl_atom r)
  | Vec (n, ts) ->
    let args = List.map ts ~f:to_glsl_atom in
    App ([%string "vec%{n#Int}"], args)
  | Mat (x, y, ts) ->
    let args = List.map ts ~f:to_glsl_atom in
    let ty =
      if x = y then [%string "mat%{x#Int}"] else [%string "mat%{x#Int}x%{y#Int}"]
    in
    App (ty, args)
  | Index (t, i) -> Index (to_glsl_atom t, i)
  | Builtin (f, args) -> Builtin (f, List.map args ~f:to_glsl_atom)
  | App (f, args) -> App (f, List.map args ~f:to_glsl_atom)
  | If (_, _, _) ->
    failwith
      [%string "to_glsl_term: complex If with condition should be handled in tr_block"]
  | Lam _ -> failwith "to_glsl_term: lambdas not yet supported"
;;

let placeholder_value_for_ty (ty : ty) : term =
  match ty with
  | TyFloat -> Float 0.0
  | TyInt -> Int 0
  | TyBool -> Bool false
  | TyVec n -> App ([%string "vec%{n#Int}"], [ Float 0.0 ])
  | TyMat (x, y) ->
    let ty =
      if x = y then [%string "mat%{x#Int}"] else [%string "mat%{x#Int}x%{y#Int}"]
    in
    App (ty, [ Float 0.0 ])
  | TyVoid -> failwith "translate: void is unsupported"
;;

let rec translate_set (map : Stlc.ty String.Map.t) (var : string) (anf : Anf.anf)
  : stmt list
  =
  match anf.desc with
  | Let (v, bind, body) ->
    let ty = to_glsl_ty (Map.find_exn map v) in
    let stmt = Decl (None, ty, v, to_glsl_term bind) in
    stmt :: translate_set map var body
  | Return t ->
    (match t.desc with
     | If (c, t, e) ->
       [ IfStmt
           ( to_glsl_atom c
           , Block (translate_set map var t)
           , Some (Block (translate_set map var e)) )
       ]
     | _ -> [ Set (Var var, to_glsl_term t) ])
;;

let rec translate_block (map : Stlc.ty String.Map.t) (anf : Anf.anf) : stmt list =
  match anf.desc with
  | Let (v, term, body) ->
    (match term.desc with
     | If (c, t, e) ->
       let ty = to_glsl_ty (Map.find_exn map v) in
       let decl = Decl (None, ty, v, placeholder_value_for_ty ty) in
       let if_stmt =
         IfStmt
           ( to_glsl_atom c
           , Block (translate_set map v t)
           , Some (Block (translate_set map v e)) )
       in
       decl :: if_stmt :: translate_block map body
     | _ ->
       let ty = to_glsl_ty (Map.find_exn map v) in
       let stmt = Decl (None, ty, v, to_glsl_term term) in
       stmt :: translate_block map body)
  | Return t ->
    (match t.desc with
     | If (c, t, e) ->
       [ IfStmt
           ( to_glsl_atom c
           , Block (translate_block map t)
           , Some (Block (translate_block map e)) )
       ]
     | _ -> [ Return (Some (to_glsl_term t)) ])
;;

let translate (Program (map, tops) : Anf.t) : t =
  let globals =
    List.map tops ~f:(fun (top : Anf.top) ->
      match top.desc with
      | Define (name, { desc = Return { desc = Lam (args, body); _ }; _ }) ->
        let ret_type =
          let rec unroll = function
            | Stlc.TyArrow (_, r) -> unroll r
            | ty -> ty
          in
          to_glsl_ty (unroll (Map.find_exn map name))
        in
        let params = List.map args ~f:(fun (arg, arg_ty) -> to_glsl_ty arg_ty, arg) in
        let body = translate_block map body in
        Function { name; desc = None; params; ret_type; body }
      | Define (_, { desc = Return _; _ }) ->
        raise_s [%message "translate: expected lam form at toplevel" (top : Anf.top)]
      | Define (_, { desc = Let _; _ }) ->
        raise_s [%message "translate: expected return toplevel" (top : Anf.top)]
      | Extern (ty, v) -> Global (Uniform, to_glsl_ty ty, v))
  in
  Program globals
;;
