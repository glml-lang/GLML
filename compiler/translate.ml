open Core
open Glsl

let to_glsl_ty (ty : Stlc.ty) : ty =
  match ty with
  | TyFloat -> TyFloat
  | TyInt -> TyInt
  | TyBool -> TyBool
  | TyVec n -> TyVec n
  | TyMat (x, y) -> TyMat (x, y)
  | TyArrow _ -> failwith "translate: arrow types should not be translated"
;;

let to_glsl_atom (a : Anf.atom) : term =
  match a with
  | Var v -> Var v
  | Float f -> Float f
  | Int i -> Int i
  | Bool b -> Bool b
;;

let to_glsl_term (t : Anf.term) : term =
  match t.desc with
  | Atom a -> to_glsl_atom a
  | Bop (op, l, r) -> Bop (op, to_glsl_atom l, to_glsl_atom r)
  | Vec (n, ts) ->
    let args = List.map ts ~f:to_glsl_atom in
    App ([%string "vec%{n#Int}"], args)
  | Mat (x, y, ts) ->
    let args = List.map ts ~f:to_glsl_atom in
    let ty =
      if x = y then [%string "mat%{x#Int}"] else [%string "mat%{x#Int}x%{y#Int}"]
    in
    App (ty, args)
  | Index (t, i) -> Index (to_glsl_atom t, i)
  | Builtin (f, args) -> Builtin (f, List.map args ~f:to_glsl_atom)
  | App (f, args) -> App (f, List.map args ~f:to_glsl_atom)
  | If (_, _, _) ->
    failwith
      [%string "to_glsl_term: complex If with condition should be handled in tr_block"]
  | Lam _ -> failwith "to_glsl_term: lambdas not yet supported"
;;

let placeholder_value_for_ty (ty : ty) : term =
  match ty with
  | TyFloat -> Float 0.0
  | TyInt -> Int 0
  | TyBool -> Bool false
  | TyVec n -> App ([%string "vec%{n#Int}"], [ Float 0.0 ])
  | TyMat (x, y) ->
    let ty =
      if x = y then [%string "mat%{x#Int}"] else [%string "mat%{x#Int}x%{y#Int}"]
    in
    App (ty, [ Float 0.0 ])
  | TyVoid -> failwith "translate: void is unsupported"
;;

let rec translate_set (var : string) (anf : Anf.anf) : stmt list =
  match anf.desc with
  | Let (v, bind, body) ->
    Decl (None, to_glsl_ty anf.ty, v, to_glsl_term bind) :: translate_set var body
  | Return t ->
    (match t.desc with
     | If (c, t, e) ->
       [ IfStmt
           ( to_glsl_atom c
           , Block (translate_set var t)
           , Some (Block (translate_set var e)) )
       ]
     | _ -> [ Set (Var var, to_glsl_term t) ])
;;

let rec translate_block (anf : Anf.anf) : stmt list =
  match anf.desc with
  | Let (v, term, body) ->
    let ty = to_glsl_ty term.ty in
    (match term.desc with
     | If (c, t, e) ->
       Decl (None, ty, v, placeholder_value_for_ty ty)
       :: IfStmt
            (to_glsl_atom c, Block (translate_set v t), Some (Block (translate_set v e)))
       :: translate_block body
     | _ -> Decl (None, ty, v, to_glsl_term term) :: translate_block body)
  | Return t ->
    (match t.desc with
     | If (c, t, e) ->
       [ IfStmt
           (to_glsl_atom c, Block (translate_block t), Some (Block (translate_block e)))
       ]
     | _ -> [ Return (Some (to_glsl_term t)) ])
;;

let translate (Program tops : Anf.t) : t =
  let globals =
    List.map tops ~f:(fun (top : Anf.top) ->
      match top.desc with
      | Define (name, { desc = Return { desc = Lam (args, body); _ }; _ }) ->
        let ret_type =
          let rec unroll = function
            | Stlc.TyArrow (_, r) -> unroll r
            | ty -> ty
          in
          to_glsl_ty (unroll top.ty)
        in
        let params = List.map args ~f:(fun (arg, arg_ty) -> to_glsl_ty arg_ty, arg) in
        let body = translate_block body in
        Function { name; desc = None; params; ret_type; body }
      | Define (_, { desc = Return _; _ }) ->
        raise_s [%message "translate: expected lam form at toplevel" (top : Anf.top)]
      | Define (_, { desc = Let _; _ }) ->
        raise_s [%message "translate: expected return toplevel" (top : Anf.top)]
      | Extern v -> Global (Uniform, to_glsl_ty top.ty, v))
  in
  Program globals
;;
